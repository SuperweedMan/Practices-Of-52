		T2CON EQU 0C8H
		TR2 EQU T2CON.2
		RCAP2L EQU 0CAH
		RCAP2H EQU 0CBH
		TH2 EQU 0CDH
		TL2 EQU 0CCH
			
		ORG 0000H
		LJMP MAIN
		ORG 0023H
		LJMP SERIES
MAIN:	MOV SP,#60H
		LCALL INIT
		LCALL SENDBUFF
HERE:	JBC F0,NEXT
		SJMP HERE
NEXT:	LCALL DELAY
		LCALL SENDBUFF
		SJMP HERE

INIT:	PUSH PSW
		PUSH ACC
		MOV TH2,#0FDH
		MOV TL2,#0F4H
		MOV RCAP2H,#0FDH
		MOV RCAP2L,#0F4H
		MOV T2CON,#30H
		MOV PCON,#80H
		MOV SCON,#0D0H
		SETB EA
		SETB ES
		SETB TR2
		POP ACC
		POP PSW
		RET

DELAY:	PUSH 30H
		PUSH 31H
		PUSH PSW
		PUSH ACC
		MOV 30H,#250
LOOP2:	MOV 31H,#250
LOOP1:	DJNZ 31H,LOOP1
		DJNZ 30H,LOOP2
		POP ACC
		POP PSW
		POP 31H
		POP 30H
		RET

SENDBUFF:PUSH PSW
		PUSH ACC
		MOV A,#0FFH
		MOV C,P
		CPL C
		MOV TB8,C
		MOV SBUF,#0FFH
		POP ACC
		POP PSW
		RET

SERIES:	JBC RI,RECEIVE
		JBC TI,SEND
		RETI
RECEIVE:PUSH ACC
		PUSH PSW
		MOV A,SBUF
		MOV C,P
		CPL C		;奇校验
		JC SE1
		JB RB8,ERR
		SJMP SE2
SE1:	JNB RB8,ERR	
SE2:	MOV P1,A
ERR:	POP PSW
		POP ACC
		RETI
SEND:	SETB F0
		RETI
		END