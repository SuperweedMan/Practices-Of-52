;30H 显示的值 31H百位 32H十位 33H个位
		ORG 0000H
		LJMP MAIN
MAIN:	MOV SP,#60H
		MOV P2,#0FFH
		MOV	30H,#154 ;显示的值
HERE:	LCALL DISPLAY
		SJMP HERE

DISPLAY:PUSH PSW
		PUSH B
		MOV A,30H
		MOV B,#100
		DIV AB
		MOV 31H,A ;百位
		MOV A,B
		MOV B,#10
		DIV AB
		MOV 32H,A ;十位
		MOV 33H,B ;个位
		MOV R2,#3  ;for循环3次
		MOV R3,#0FEH
		MOV R0,#31H	;选择位
		MOV DPTR ,#TABLE
LOOP:	MOV A,@R0
		MOVC A,@A+DPTR
		MOV P2,R3	;片选
		MOV P0,A	;段码

		INC R0
		MOV A,R3
		RL A
		MOV R3,A
		LCALL DELAY
		DJNZ R2,LOOP
		POP B
		POP PSW
		RET

DELAY:	MOV A,R6
		PUSH ACC
		MOV A,R7
		PUSH ACC
		MOV R6,#20
DELAY1:	MOV R7,#24
		DJNZ R7,$
		DJNZ R6,DELAY1
		POP ACC
		MOV R7,A
		POP ACC
		MOV R6,A
		RET

TABLE:	DB 3FH,06H,5BH,4FH,66H,6DH,7DH,07H,7FH,6FH		
		END
